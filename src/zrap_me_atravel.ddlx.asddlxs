@Metadata.layer: #CORE
@Search.searchable: true
@UI : {
    headerInfo: {
    typeName: 'Travel',
    typeNamePlural: 'Travels',
    title: {
        type: #STANDARD,
        value: 'travel_id'
    }
}
}
annotate entity ZRAP_CDS_C_ATRAVEL with
{
  @UI.facet: [{ id: 'Travel',
                 purpose: #STANDARD,
                 type: #IDENTIFICATION_REFERENCE,
                 label: 'Travel',
                 position: 10 },
                { id : 'Booking',
                  purpose: #STANDARD,
                  type: #LINEITEM_REFERENCE,
                  label: 'Booking',
                  position: 20,
                  targetElement: '_Booking'
                 }]

  @UI: {
      lineItem: [{
          position: 10,
          importance: #HIGH
      }],
      identification: [{
      position: 10,
      label: 'Travel ID [1,...,999999]'
      }]
  }
  @Search.defaultSearchElement: true
  travel_id;

  @UI: {
    lineItem: [{
        position: 20,
        importance: #HIGH
    }],
    identification: [{
        position: 20
    }],
    selectionField: [{
        position: 20
    }]
  }
  @Consumption.valueHelpDefinition: [{
    entity: {
          name: '/DMO/I_Agency',
          element: 'AgencyID'
      }
   }]
  @Search.defaultSearchElement: true
  agency_id;

  @UI: {
    lineItem: [{
        position: 30,
        importance: #HIGH
    }],
    identification: [{
        position: 30
    }],
    selectionField: [{
        position: 30
    }]
  }
  @Consumption.valueHelpDefinition: [{
    entity: {
          name: '/DMO/I_Customer',
          element: 'CustomerID'
      }
   }]
  @Search.defaultSearchElement: true
  customer_id;

  @UI: {
    identification: [{
        position: 40
    }]
  }
  begin_date;

  @UI: {
  identification: [{
    position: 50
  }]
  }
  end_date;

  @UI: {
   lineItem: [{
       position: 60,
       importance: #MEDIUM
   }],
   identification: [{
       position: 60
   }]
  }
  booking_fee;

  @UI: {
    lineItem: [{
        position: 70,
        importance: #MEDIUM
    }],
    identification: [{
        position: 70,
        label: 'Total Price'
    }]
  }
  total_price;

  @Consumption.valueHelpDefinition: [{
    entity: {
          name: 'I_Currency',
          element: 'Currency'
      }
  }]
  currency_code;

  @UI : {
  lineItem: [{
    position: 80,
    importance: #HIGH
    },
    { type: #FOR_ACTION,
      dataAction: 'acceptTravel',
      label : 'Accept Travel'
    },
    { type: #FOR_ACTION,
      dataAction: 'rejectTravel',
      label : 'Reject Travel'
    }],
   identification: [{
      position: 80
    },
    { type: #FOR_ACTION,
      dataAction: 'acceptTravel',
      label : 'Accept Travel'
    },
    { type: #FOR_ACTION,
      dataAction: 'rejectTravel',
      label : 'Reject Travel'
    }],
    selectionField: [{ position: 40 }]
  }
  @EndUserText.label: 'Overall Status'
  overall_status;

  @UI : {
  lineItem: [{
      position: 90,
      importance: #MEDIUM
   }],
   identification: [{ position: 90 }]
  }
  description;

  @UI.hidden: true
  last_changed_at;

}