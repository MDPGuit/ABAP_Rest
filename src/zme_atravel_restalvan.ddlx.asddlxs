@Metadata.layer: #CORE
@Search.searchable: true
@UI : {
    headerInfo: {
    typeName: 'Travel',
    typeNamePlural: 'Travels',
    title: {
        type: #STANDARD,
        value: 'TravelID'
    }
}
}
annotate entity ZCDS_C_ATRAVEL_RESTALVAN with
{
  @UI.facet: [{ id: 'Travel',
                 purpose: #STANDARD,
                 type: #IDENTIFICATION_REFERENCE,
                 label: 'Travel',
                 position: 10 },
                { id : 'Booking',
                  purpose: #STANDARD,
                  type: #LINEITEM_REFERENCE,
                  label: 'Booking',
                  position: 20,
                  targetElement: '_Booking'
                 }]

  @UI: {
      lineItem: [{
          position: 10,
          importance: #HIGH
      }],
      identification: [{
      position: 10,
      label: 'Travel ID [1,...,999999]'
      }]
  }
  @Search.defaultSearchElement: true
  TravelId;


  @UI: {
    lineItem: [{
        position: 20,
        importance: #HIGH
    }],
    identification: [{
        position: 20
    }],
    selectionField: [{
        position: 20
    }]
  }
  @Consumption.valueHelpDefinition: [{
    entity: {
          name: '/DMO/I_Agency',
          element: 'AgencyID'
      }
   }]
  @Search.defaultSearchElement: true
  AgencyId;

  @UI: {
    lineItem: [{
        position: 30,
        importance: #HIGH
    }],
    identification: [{
        position: 30
    }],
    selectionField: [{
        position: 30
    }]
  }
  @Consumption.valueHelpDefinition: [{
    entity: {
          name: '/DMO/I_Customer',
          element: 'CustomerID'
      }
   }]
  @Search.defaultSearchElement: true
  CustomerId;

  @UI: {
    identification: [{
        position: 40
    }]
  }
  BeginDate;


  @UI: {
    identification: [{
        position: 50
    }]
  }
  EndDate;

  @UI: {
    lineItem: [{
        position: 60,
        importance: #MEDIUM
    }],
    identification: [{
        position: 60
    }]
  }
  BookingFee;

  @UI: {
    lineItem: [{
        position: 70,
        importance: #MEDIUM
    }],
    identification: [{
        position: 70,
        label: 'Total Price'
    }]
  }
  TotalPrice;

  @Consumption.valueHelpDefinition: [{
    entity: {
          name: 'I_Currency',
          element: 'Currency'
      }
  }]
  CurrencyCode;

  @UI : {
    lineItem: [{
      position: 80,
      importance: #HIGH
      },
      { type: #FOR_ACTION,
        dataAction: 'acceptTravel',
        label : 'Accept Travel'
      },
      { type: #FOR_ACTION,
        dataAction: 'rejectTravel',
        label : 'Reject Travel'
      }],
     identification: [{
        position: 80
      },
      { type: #FOR_ACTION,
        dataAction: 'acceptTravel',
        label : 'Accept Travel'
      },
      { type: #FOR_ACTION,
        dataAction: 'rejectTravel',
        label : 'Reject Travel'
      }],
      selectionField: [{ position: 40 }]
  }
  @EndUserText.label: 'Overall Status'
  OverallStatus;
  
  @UI : { 
    lineItem: [{ 
        position: 90,
        importance: #MEDIUM
     }],
     identification: [{ position: 90 }]
  }
  Description;
  

  @UI.hidden: true
  LastChangedAt;

}